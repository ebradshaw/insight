apply plugin: 'groovy'  //For testing

configurations {
    jarjar
}

dependencies {
    compile ('com.google.code.java-allocation-instrumenter:java-allocation-instrumenter:3.0.1') { transitive = false }

    compile 'org.ow2.asm:asm:5.2'
    compile 'org.ow2.asm:asm-analysis:5.2'
    compile 'org.ow2.asm:asm-commons:5.2'
    compile 'org.ow2.asm:asm-util:5.2'

    testCompile project(":agent-runtime")
    testCompile 'javax.servlet:javax.servlet-api:3.0.1'
    testCompile 'com.ea.agentloader:ea-agent-loader:1.0.3'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.12'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'

    jarjar 'org.pantsbuild:jarjar:1.6.4'
}

jar {
    archiveName "insight-agent.jar"
    from(project(":agent-runtime").buildDir) {
        include "libs/**"
    }
    doLast {
        project.ant {
            taskdef name: 'jarjar', classname: 'org.pantsbuild.jarjar.JarJarTask', classpath: configurations.jarjar.asPath
            jarjar(jarfile: jar.archivePath, manifest: "build/resources/main/META-INF/MANIFEST.MF", update: true) {
                zipfileset(src: jar.archivePath)
                configurations.compile.files.collect { zipfileset(src: it) }
                rule pattern: 'com.google.**', result: 'com.ebradshaw.insight.agent.ext.com.google.@1'
            }
        }
    }
}

jar.dependsOn(':agent-runtime:build')

